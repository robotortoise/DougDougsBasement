; The final boss scene. It's a unique file for easier testing.
; "Gerald Fear" is the internal name for evil Gerald Ford.
; Elgrim possesses Gerald Ford. Show some sort of visual effect to indicate this - a flash of light or something.

; DUMMIED OUT FOR TESTING. PLEASE FIX BEFORE RELEASE!!!
; 
; 
; 
; 
; LOTS OF COMMENTS SO YOU REMEMBER THIS
@hideUI PlayCreditsButtonMenu
@processInput false
@back BjornRoom_OurpleMist id:BjornOffice time:2
@sfx sfx_blackout_lights
@back BjornRoom_OurpleMist_Off id:BjornOffice time:2
@sfx sfx_earthquake fade:0.5
@spawn FordSmoke wait:false
@sfx sfx_strong_wind fade:1 volume:0.5
@shake Camera count:35 time:0.2 deltaTime:0.1 power:0.2 deltaPower:0.1 hor:true ver:false wait:false
@char GeraldFear.Shadow wait:false scale:1 pose:SemiTransparent
@sfx sfx_major_destruction volume:0.2 wait:false
@back BjornRoom_OurpleMist_Pentagram id:BjornOffice tint:purple time:4
@sfx sfx_fear_laugh_oneshot
@back id:BlackBG transition:CircleReveal time:3
@wait 2
@stopSfx sfx_earthquake fade:1 wait:false
@stopSfx sfx_strong_wind fade:1 wait:false
@stopSfx sfx_major_destruction fade:1.5
@hideAll time:0
@despawn FordSmoke wait:false
@wait 2
@back GeraldRoom id:GeraldOffice time:4
; show Gerald Ford in shadow
@char GeraldFear.Shadow pose:Speaking time:.5
@char GeraldFear.ShadowWithBees pose:Speaking time:.2
@char GeraldFear.Normal
@processInput true
; play bee sfx.
; show Gerald Ford's evil form
GeraldFear: Doug Douggity-Damn...|#147|
@wait 2
@bgm BGM_GeraldFordIntro
@hide GeraldFear,EvilPrinter
@char Sam.Surprised
Sam: He...!<br>He didn't...!|#148|
@hide Sam,AnimatedPrinter
; Rosa angry sprite
@char Rosa.Sad
Rosa: ...|#149|
@hide Rosa,AnimatedPrinter
@char Bjorn.Angry
Bjorn: This is... |#24b|<i>entirely</i> unprofessional conduct!|#14a|
@hide Bjorn,AnimatedPrinter
; set name to Elgrim
@hide Sam,Rosa,Bjorn
@char GeraldFear
@set GeraldFear="Gerald Fear"
@shake AnimatedPrinter
GeraldFear: Fear me, mortals!|#14b|
GeraldFear: <shake>Oooooh...</shake><br>This body is so much younger and <wave>sexier</wave>, too!|#24c|[wait .5]<br>And it's all thanks to the power of High Demon Elgrim!|#14c|
@wait .3
GeraldFear: So much power...!|#25c|[wait .3]<br>I can feel it surging throughout my flesh!|#14d|
GeraldFear: This world shall rue the day it scorned <i>Gerald Ford!</i>|#14e|
@hide GeraldFear,EvilPrinter
@char Luther.Mad2
Luther: No one scorned you, asshole!|#14f|
@hide Luther,AnimatedPrinter
@char GeraldFear.Normal
@stopBgm fade:1
@bgm BGM_MoebiusBattle_Loop intro:BGM_MoebiusBattle_Start
; Gerald floats or moves somehow.
GeraldFear: Now, to get out of this blasted basement.|#150|
@wait .3
@hide GeraldFear,EvilPrinter
@char Bjorn.Angry
Bjorn: What the <i>fuck</i> did you just call it?!|#151|
@hide Bjorn,AnimatedPrinter
@char GeraldFear
GeraldFear: Ah, yes.|#24d|[wait .2]<br>Horse.|#152|
@wait .2
@hide GeraldFear,EvilPrinter
@show Bjorn
Bjorn: A <shake>horse?!</shake><br>Did you just call me a goddamn <i>horse<i>?!|#153|
@hide Bjorn,AnimatedPrinter
@show GeraldFear
GeraldFear: I believe that you have a key for me.|#154|
@hide GeraldFear,EvilPrinter
@show Bjorn
Bjorn: Why the flying unicorn fuck would I give you anything?|#251|[wait .4]<br>Also, I am <i>not</i> a goddamn-|#155|
@hide Bjorn,AnimatedPrinter time:.1
@char GeraldFear time:.2
GeraldFear: I literally do not care what you are, whelp.|#24e|[wait .3]<br>If you do not give me what I desire, |#24f|[wait .2]then...|#156|
@wait .4
@hide EvilPrinter,GeraldFear
@wait .5
@sfx BjornHorny
@wait 1
@char Bjorn.AngryHornless Pose:speaking
@sfx SFX_Piano_Stab_03 volume:.3 wait:false
; a magical burst of energy surges forth, covering Bjorn's sprite. Bjorn's sprite fades away, and when he returns, he does not have a horn.
@wait .3
Bjorn: <shake>My h-horn...!</shake><br>|#25d|[wait .3]H-how did you...?|#157|
@hide Bjorn,AnimatedPrinter
@char GeraldFear.Normal
GeraldFear: Now the only one that's feeling |#25e|[wait .3]<i>horny</i>|#25f|[wait .3] is... <i>Gerald Ford!</i>|#158|
@wait .5
@hide GeraldFear,EvilPrinter
@char Sam.Surprised
Sam: What the hell?|#159|
; Luther horror with tear expression
@char Luther.SurprisedWithTear
Luther: That's...!|#15a|
@wait .4
@hide Luther,Sam,AnimatedPrinter
@char GeraldFear.Normal
GeraldFear: I meant what I said.<br>I'm a demon, not a pansy.|#15b|
@wait .5
GeraldFear: Now, to finish the job...|#15c|
@hide GeraldFear,EvilPrinter
@wait .3
@char Bjorn.AngryHornless
@spawn FordSmoke scale:.7 position:-5,2
@sfx sfx_strong_wind volume:.5 wait:false
GeraldFear: Say your last words, Bjorn the Uni-cuck.|#15d|
@wait .5
Bjorn: <shake>Augh...!</shake>|#260|
@wait 1
@hideAll
@back id:WhiteBG wait:false
@despawn FordSmoke wait:false
@stopSfx sfx_strong_wind wait:false
@stopBgm wait:false
@sfx RosaAppears2 wait:false
@wait 2
@hide WhiteBG remove:true
@bgm PMTTYD_CrystalStars_Loop intro:PMTTYD_CrystalStars_Intro
@back GeraldRoom id:GeraldOffice wait:false
@char Bjorn.AngryHornless
@char Rosa.Smug pose:Speaking position:0.00,-6.00,20.00 wait:false
Rosa: Don't you worry, Professor Bjorn!<br>I've got you!|#15e|
Rosa dove in front of Bjorn, blocking the ex-president's path.|#268|
@wait .3
@wait .3
@hide AnimatedPrinter
@char Bjorn.HappyHornless
BjornNoFade: Rosa, you would risk your life for mine?<br>What a good apprentice you are!|#160|
@wait .3
Rosa: What you're doing here...|#261|[wait .3][char Rosa.Mad]<br>It isn't right, Mr. President!<br>|#262|[char Rosa.Normal]Murder is wrong, no matter your warped logic behind it!|#162|
@wait .3
@hide Rosa,AnimatedPrinter,Bjorn
@wait .3
@char Luther.Dismayed
Luther: Even if it's Professor Bjorn?|#284|
@hide Luther,AnimatedPrinter
@show Bjorn,Rosa
@char Rosa.Happy wait:false
Rosa: Even if it's Professor Bjorn!|#285|
@wait .3
@hide Rosa,Bjorn,AnimatedPrinter
@char GeraldFear
@stopBgm PMTTYD_CrystalStars_Loop fade:1
GeraldFear: It's not murder if he's going to come back as a ghost, Rosa.<br>Duh.|#164|
@hide GeraldFear,EvilPrinter
@show Bjorn,Rosa
@char Rosa.Annoyed
Rosa: .|#263|[wait .3].|#264|[wait .3].|#165|
@char Bjorn.AngryHornless
Bjorn: Rosa, I thought you were defending me because you had an attachment to me.|#166|
@wait .3
@char Rosa.Mad
Rosa: Oh, goodness no. You're extremely unethical.|#292|
Rosa: I just did it because it was the right thing to do.|#167|
@wait .3
@char Bjorn.NormalHornless
Bjorn: .|#265|[wait .3].|#266|[wait .3].|#168|
Bjorn: Oh.|#267|
@wait 1.5
@hide Bjorn,Rosa,AnimatedPrinter
@wait .3
@bgm BGM_MoebiusBattle_Loop intro:BGM_MoebiusBattle_Start
@char GeraldFear.Normal
GeraldFear: Anyway...|#269|
GeraldFear: If you don't move, then I guess I'll have to kill you both!|#26a|
@wait .3
@hide GeraldFear,EvilPrinter
@char Sam.Angry
Sam: You can't get away from me this time, Elgrim!|#16a|
@hide Sam,AnimatedPrinter
@char GeraldFear.Normal
GeraldFear: It's <i>Ford</i>, damn you!<br>Gerald Ford!|#16b|
@wait .3
@hide GeraldFear
@char Sam.Angry
; Sam kicks Ford. play punching sfx, shake GeraldFear sprite.
; can you play the gmod ragdoll sfx here
Sam: Hi-yah!|#16c|
@hide Sam,AnimatedPrinter
@char GeraldFear.Normal
@sfx PunchingMyFord
@hide Rosa remove:true
@hide Bjorn remove:true
@shake GeraldFear count:2 deltaPower:3 wait:false
; play punch sfx
@hide EvilPrinter
@stopBgm BGM_MoebiusBattle_Loop wait:false fade:2
@wait 1
GeraldFear: .|#293|[wait .3].|#294|[wait .3].|#16d|
@wait 1
@hide EvilPrinter,GeraldFear
Player: .|#295|[wait .3].|#296|[wait .3].|#16e|
@hide EvilPrinter
@wait .2
@char Pringle.Mad
Pringle: And you did that, because...?|#16f|
@wait .5
@char Sam.Surprised
Sam: Well, ah...<br>I thought I was going to hit him harder than that.|#170|
@wait .3
@hide Sam,AnimatedPrinter,Pringle
@char GeraldFear.Normal
GeraldFear: That was very foolish of you, boy.|#171|
@wait .2
@hide GeraldFear,EvilPrinter
@char Sam.Sad
Sam: Uh-oh.|#172|
@wait .5
@hide Sam,AnimatedPrinter
@char GeraldFear.Normal
GeraldFear: You know, I was going to get my bearings, maybe set a few houses on fire in my hometown of Omaha, Nebraska.|#173|
@bgm BGM_GF_PEPSIMAN_Loop intro:BGM_GF_PEPSIMAN_Intro
GeraldFear: But that was then, and this is now.|#174|
@wait .5
@bgm BGM_GF_PEPSIMAN_Loop intro:BGM_GF_PEPSIMAN_Intro
GeraldFear: Do you wish to join your younger brothers, Sam?<br>|#286|<shake>Because I'll lead you right to 'em!</shake>|#175|
@wait .2
@hide GeraldFear,EvilPrinter
@char Sam.Angry
Sam: You...!<br>|#289|[wait .5]Don't tell me <i>you</i> were the one who...!|#176|
@wait 1
@hideChars
PlayerInternal: I can't just stand here and watch!<br>I need to do something!|#177|

# FightDemonPresStart
@char GeraldFear.Normal
PlayerInternal: What should I do...?|#178|
@set g_choice_enabled=!chose_fightdemonpres_taunt
@choice "Taunt the former president.|#179|" goto:.TauntDemonPres
@set g_choice_enabled=!chose_fightdemonpres_askwhy
@choice "Ask him why he's not demon-speaking like Sam.|#17a|" goto:.WhyDemonPresNotSpeakBad
@set g_choice_enabled=FinalBossOil||!(chose_fightdemonpres_inv_gun&&chose_fightdemonpres_inv_pringle)
@choice "Use something from my inventory.|#17b|" goto:.DemonPresInventory
@set g_choice_enabled=true
@choice "Use the  \"holey oil\" on the former president.|#17c|" goto:.StartFordKiss if:(FinalBossOil&&shot_holey_oil)
; wait for player input
@stop

# TauntDemonPres
@set chose_fightdemonpres_taunt=true
Player: Hey, uh...|#17d|
@char GeraldFear.Normal
GeraldFear: Yes?|#17e|
Player: Uh....|#26b|[wait .5]<br>Uhhhhhhhhh.............|#17f|
Player: Um, this is uncomfortable.|#180|
GeraldFear: .|#26d|[wait .3].|#26e|[wait .3].|#181|
Player: I was going to try and make fun of you, but...|#182|
@wait .2
Player: Ah, <i>dammit</i>!<br>I totally blanked on your name.<br>Sorry, what was it again?|#183|
GeraldFear: <shake>GRAAAAUGH!</shake><br>How <i>dare</i> you!|#184|
Player: He didn't like that.<br>Good to know.... I think.|#185|
GeraldFear: <shake>I'll fucking |#287|[shake GeraldFear]<b>kill</b> you, |#26c|{PlayerName}</shake>!!|#186|
@hideChars
@goto .FightDemonPresStart

# WhyDemonPresNotSpeakBad
@set chose_fightdemonpres_askwhy=true
Player: Hey, Geronimo.|#187|
@char GeraldFear.Normal
GeraldFear: Ugh. What is it now?<br>Come on, get it over with before I kill you.|#188|
Player: Why aren't you speaking all funny like Sam was?|#189|
GeraldFear: That's because I was eating Sam.<br>This is more of a full merge – I am Ford, yet I am also Elgrim.|#18a|
Player: So, it's like a fu...|#18b|
GeraldFear: Call it a <i>fusion dance</i> and I vaporize you.|#18c|
@hide GeraldFear,EvilPrinter
@char Luther.Dismayed
Luther: I don't think he can actually do that, |#18d|{PlayerName}.|#18e|
@char Rosa.Normal
Rosa: Yes, that doesn't make sense.<br>If he could vaporize us, he would have already done so.|#18f|
@hide Rosa,Luther,AnimatedPrinter
@char GeraldFear.Normal
GeraldFear: I don't need to hear it from the sea nut gallery.|#190|
@wait .5
@hide GeraldFear,EvilPrinter
@char Sam.Smug
Sam: Sea?<br>More like...|#26f|
@wait .1
Sam: Sea deez nuts on your face!|#191|
@wait .3
@hide Sam,AnimatedPrinter
@char GeraldFear.Normal
GeraldFear: .|#270|[wait .3].|#271|[wait .3].|#272|
GeraldFear: What?|#192|
@hide GeraldFear,EvilPrinter
@char Sam.Happy
Sam: Hah!<br>Got 'em!|#193|
@hide Sam,AnimatedPrinter
@char Rosa.Mad
Rosa: .|#273|[wait .3].|#274|[wait .3].|#194|
@hide Rosa
@wait .4
@char Bjorn.AngryHornless
Bjorn: .|#275|[wait .3].|#276|[wait .3].|#195|
@hide Bjorn,AnimatedPrinter
Player: Er, is Sam still-|#196|
@char GeraldFear.Normal
GeraldFear: No, this isn't my doing.<br>Sam's just weird.|#197|
@wait .5
PlayerInternal: Guess the ex-president is still half-Elgrim...|#198|
PlayerInternal: Is that information useful to me?<br>How'd I defeat him last time?|#199|
@hideChars
@goto .FightDemonPresStart

# DemonPresInventory
@set chose_fightdemonpres_inv_gun=(chose_fightdemonpres_gunopt1&&chose_fightdemonpres_gunopt2)
@hide Pringle
PlayerInternal: What should I use...?|#19a|
@set g_choice_enabled=(!chose_fightdemonpres_inv_gun)||FinalBossOil
@choice "Use the gun.|#19b|" goto:.DemonPresGunInventory
@set g_choice_enabled=!chose_fightdemonpres_inv_pringle
@choice "Talk to Pringle.|#19c|" goto:.TalkToPringle
@set g_choice_enabled=true
@choice "Return to the previous menu.|#19d|" goto:.FightDemonPresStart
@stop

# DemonPresGunInventory
@hideChars
PlayerInternal: What should I use the gun on?|#19e|
@set g_choice_enabled=!chose_fightdemonpres_gunopt1
@choice "Jerry Ford.|#19f|" goto:.GunOpt1
@set g_choice_enabled=!chose_fightdemonpres_gunopt2
@choice "Luther.|#1a0|" goto:.GunOpt2
@set g_choice_enabled=!chose_fightdemonpres_gunopt3
@choice "The oil puddle.|#1a1|" goto:.GunOpt3 if:FinalBossOil
@set g_choice_enabled=true
@choice "Return to the previous menu.|#1a2|" goto:.DemonPresInventory
@hideChars
; wait for player input.
@stop

# GunOpt1
@set chose_fightdemonpres_gunopt1=true
@char GeraldFear.Normal
Player: Demon or not, Garry Ford is still a former president.<br>I can't shoot him!|#1a3|
@hide GeraldFear
@wait .3
@char Pringle.Mad
Pringle: You were thinking of <i>shooting him?</i>|#1a4|
Player: I...|#277|[wait .4] I wasn't!<br>That's why I said I wasn't going to do it!|#1a5|
@char Pringle.Happy
Pringle: Then why'd you mention it?|#278|
PlayerInternal: He's right.|#279|
Player: I can't just say things like that out loud.<br>This isn't a point-and-click adventure game.|#1a6|
@char Sam.Happy
Sam: Right.<br>That's <i>my</i> territory.|#1a7|
@char Luther.Happy
Luther: And mine.|#1a8|
@hide Luther,Sam,Pringle
; This is a real game btw
@char Bjorn.HappyHornless
Bjorn: <i>My</i> favorite game is <i>Jeopardy!</i> for the Phillips CD-i.|#1a9|
@wait .5
@hide Bjorn,AnimatedPrinter
@char Luther.Dismayed
Luther: .|#27a|[wait .3].|#27b|[wait .3].|#1aa|
@wait .2
@hide Luther,AnimatedPrinter
@char GeraldFear.Normal
GeraldFear: ...|#1ab|
@wait .2
@hide GeraldFear,EvilPrinter
@char Bjorn.NormalHornless
Bjorn: What?<br>No one ever asks about my interests.|#1ac|
@wait 1.5
@hideChars
@goto .DemonPresInventory

# GunOpt2
@set chose_fightdemonpres_gunopt2=true
@char Luther.Normal pose:NotSpeaking
PlayerInternal: As much as I'd love to shoot Luther, I don't think doing that would help in this specific situation.|#1ad|
@wait .2
PlayerInternal: ...|#1ae|
@wait .2
PlayerInternal: Although...|#1af|
@wait .2
; show Luther's sprite
@wait .2
; Frankendoodle reference
PlayerInternal: He's hideous! He makes me sick just looking at him.|#1b0|
@wait .2
@char Luther.Normal
Luther: Why are you staring at me?<br>What'd I do this time?|#1b1|
Player: Nothing... |#27d|[wait .2]unfortunately.|#27c|
; Luther confused sprite
@char Luther.Surprised
Luther: ...?|#1b4|
@wait 1.5
@hideChars
@goto .DemonPresInventory

# GunOpt3
@set chose_fightdemonpres_gunopt3=true
Player: I need "holey" oil, huh...?|#1b5|
; play gun shooting at a puddle of water sfx
I pulled out my gun, aiming at the oil puddle.|#1b6|
@hide AnimatedPrinter
@set shot_holey_oil=true
@sfx Gunshoot wait:false
@back GeraldRoom_Oil_Bullets id:GeraldOffice time:5
@char Rosa.Normal
Rosa: Goodness!<br>What was that for, |#1b7|{PlayerName}?!|#1b8|
@hide Rosa,AnimatedPrinter
@char Bjorn.AngryHornless
Bjorn: Stale marshmallows, |#28a|{PlayerName}!|#1b9|
Player: Oh, forgot to reload.|#28b|
@sfx ReloadGun volume:.5
@wait .2
Bjorn: Watch where you're aiming that thing, |#1ba|{PlayerName}!|#1bb|
Player: Oh, don't worry.<br>I'm aiming exactly where I need to.|#1bc|
Bjorn: What?<br>That doesn't explain anything at all!|#1bd|
@hide Bjorn,AnimatedPrinter
@wait .2
@char GeraldFear.Normal
GeraldFear: ...|#1be|
@wait 1.5
@hideChars
@goto .FightDemonPresStart



# TalkToPringle
@hideChars
Player: Hey, Pringle.|#1bf|
@char Pringle.Normal
Pringle: Hi, |#1c0|{PlayerName}!<br>What's sizzling?|#1c1|
@goto .PringleQStart

# PringleQStart
@set chose_fightdemonpres_inv_pringle=(chose_fightdemonpres_pringle1&&chose_fightdemonpres_pringle2)
Player: I have a question for you.|#1c2|
@char Pringle.Normal
@set g_choice_enabled=!chose_fightdemonpres_pringle1
@choice "Can I have some of your oil, please?|#1c3|" goto:.PringleQ1
@set g_choice_enabled=!chose_fightdemonpres_pringle2
@choice "Does your pan regenerate like the rest of your body?|#1c4|" goto:.PringleQ2
@set g_choice_enabled=true
@choice "Return to the previous menu.|#1c5|" goto:.DemonPresInventory
; wait for player input.
@stop

# PringleQ1
@set chose_fightdemonpres_pringle1=true
Player: The last time we fought him,<br>Elgrim was defeated by holy oil.|#1c6|
@wait .2
Player: The oil inside your pan... it's not holy, is it?|#1c7|
@char Pringle.Love
Pringle: No, this oil is all produced by yours truly.|#1c8|
@wait .2
Pringle: You know how the human face secretes oil?<br>It's kind of like that.|#1c9|
PlayerInternal: Eww.|#27e|
Player: Well, I'm a pepper, so I'm not too familiar with human skin.|#1ca|
@char Pringle.Normal
Pringle: It's not holy.<br>|#27f|[char Pringle.Laughing]But I'll totally let you use some of it if you need some!|#1cb|
Pringle: Are you trying to use it against Elgrim?|#1cc|
Player: Yeah.<br>Would you be able to drip some of it on the ground for me?|#1cd|
@char Pringle.Normal
Pringle: Sure, no sweat!|#1ce|
; set "final boss oil" flag to "true". This needs to be true before the shoot oil prompt appears.
@set FinalBossOil=true
@hide Pringle,AnimatedPrinter
@sfx sfx_oil_pour
@back GeraldRoom_Oil id:GeraldOffice time:3
Pringle poured some of his oil on the ground.<br>Then, he hopped back into my inventory.|#1cf|
@wait 1
@show Pringle
; Pringle sprite fades out, then back in
@char Pringle.Happy
Pringle: Piece of cake — literally!|#1d0|
@wait .2
Pringle: Get it?|#280|
Pringle: Because you need cooking oil to make a cake?|#1d1|
@char Pringle.Normal
Player: Yeah, I got it.|#1d2|
PlayerInternal: Now that there's some oil spilled on the ground...<br>What's next?|#1d3|
@hideChars
@goto .PringleQStart

# PringleQ2
@set chose_fightdemonpres_pringle2=true
Player: Pringle, can I ask you a weird question?|#1d4|
@char Pringle.Excited
Pringle: You can ask me anything!|#1d5|
Player: Okay, awesome.<br>|#281|[wait .3]Can I shoot you?|#1d6|
@char Pringle.Normal
Pringle: .|#282|[wait .3].|#283|[wait .3].|#1d7|
Pringle: .....................|#1d8|
Pringle: Like, with-|#1d9|
Player: With a gun, yeah.|#1da|
Pringle: ..................|#1db|
@wait .4
@char Pringle.Mad
Pringle: But, why...?|#1dc|
Player: I just thought...|#1dd|
@wait .3
Player: Um, does...|#1de|
Player: If I shot you, would you be okay afterwards?|#1df|
Pringle: ...|#1e0|
@char Pringle.Normal
Pringle: Are you mentally well, |#1e1|{PlayerName}?<br>Have you eaten today?|#1e2|
@char Pringle.Happy
Pringle: I have some food if you haven't, you know.|#1e3|
Player: I...<br>W-well, like, if I shot you, then the oil would be full of holes.<br>And if your pan could regenerate, then...|#1e4|
@char Pringle.Normal
Pringle: ...|#1e5|
Pringle: Sorry, but I'm not letting you shoot me.|#1e6|
Player: Damn.<br>Good thing I asked first.|#1e7|
Pringle: ...|#1e8|
Pringle: You are a strange one, |#1e9|{PlayerName}.|#1ea|
PlayerInternal: The pan is calling the kettle black.|#1eb|
@hideChars
@goto .PringleQStart

# StartFordKiss
@stopBgm BGM_MoebiusBattle_Loop
; kill the music
Player: This can't go on.<br>I have to save my friends, and this world.|#1ec|
@wait .3
Player: I need to end you, Elgrim!|#1ed|
@char GeraldFear.Normal
; This is said sarcastically
GeraldFear: Oh, of <i>course</i> Elgrim is the dangerous part of me.|#1ee|
@wait .3
GeraldFear: <i>Gerald Ford</i> can't even be a little threatening, huh?|#1ef|
@hide EvilPrinter remove:true wait:false
@hide GeraldFear wait:false
@back id:BlackBG
@print "I crouched down low, bracing myself.|#28c|" printer:AnimatedPrinter
@print "I closed my eyes as I licked the puddle of holey oil.|#28d|" printer:AnimatedPrinter
@wait 1.5
It tasted of ash, soot, and gunpowder.|#1f2|
@hide AnimatedPrinter
@hide BlackBG
@char GeraldFear.Normal
GeraldFear: Ew...|#1f3|
@wait .3
@hide GeraldFear,EvilPrinter
@char Rosa
Rosa: That's disgusting.|#1f4|
@wait .3
@hide Rosa remove:true
@hide AnimatedPrinter
@stopBgm fade:1
@wait 1.5
@bgm BGM_BlueLineAll
Player: Gerald...|#1f5|
@char GeraldFear.Normal
@shake EvilPrinter
GeraldFear: ?!|#1f6|
GeraldFear: Y-You...|#252|[shake EvilPrinter]<br>Y-You said my name...!|#1f7|
@print "I knew what I had to do.|#28f|" printer:AnimatedPrinter
@print "The only question was if I had the strength to go through with it.|#290|" printer:AnimatedPrinter
@print "There was one way to find out.|#291|" printer:AnimatedPrinter
@wait 1.5
@choice "Ask the ex-president if you can kiss him.|#1fc|"

@stop
; play chimes sound effect like this (and I am SO sorry for using this as the reference) https://youtu.be/ZgJHdRDvPCw?si=5Q4XIcHj3tnUnie6&t=109
Player: Mr. President...|#1fd|
Player: May I...|#257|[wait 1.5] k|#253|[wait .3]i|#254|[wait .3]s|#255|[wait .3]s|#256|[wait .3] you?|#1ff|
@wait 1.5
PlayerInternal: Wait, what was his full name again?|#200|
@wait 1.5
@set g_choice_enabled=true
@choice "Gerald Ford.|#201|" goto:.SteamyMakeoutSesh
@choice "Gerald Ford!|#202|" goto:.SteamyMakeoutSesh
@choice "Gerald Ford~ mwah~|#203|" goto:.SteamyMakeoutSesh
;wait for player input
@stop


# SteamyMakeoutSesh
@if shot_holey_oil=true
    @back GayLighting_Oil_Bullets id:GayLighting time:4 wait:false
    @endIf

@if shot_holey_oil=false
    @back GayLighting_Oil id:GayLighting time:4 wait:false
    @endIf

@char GeraldFear.Normal
GeraldFear: [shake EvilPrinter]<wave>K-Kiss...?!</wave>|#204|[wait .3]<br>You w-want to...|#205|
GeraldFear: You want to kiss... |#206|[wait .3]<shake><i>m-me</i></shake>?!|#207|
@wait .5
Player: Yes I do, |#208|[wait .3]<i>Gerald Ford</i>.|#209|
@wait .5
; Gerald Ford moans a little too. This whole exchange should sound slightly sexual for maximum humor. Reference: https://www.youtube.com/clip/UgkxAIRGS8FymdtHH30wWtTRHdtsAVrjlLR8
GeraldFear: <i>O-oh</i>,|#20a|[wait .3] fuck.<br>T-that's...|#20b|
@sfx HornyPresSFX
@char GeraldFear.Horny
GeraldFear: P-please!<br>Say my name again!|#20c|
@wait 2.0
Player: I want to kiss you, <b>Gerald Ford</b>!|#20d|
@wait 1.5
GeraldFear: B-Being kissed...|#20e|
@wait .3
GeraldFear: .|#297|[wait .3].|#298|[wait .3].|#20f|
GeraldFear: You said my name!|#299|[wait .3]<br>You... |#288|[wait .3]said <i>my name</i>!|#210|
Player: I sure did, <i>Gerald Ford!</i>|#211|
@wait 1.5
@shake EvilPrinter count:2
GeraldFear: Oh, <i>wow</i>...|#29a|[wait .3]<br>Hearing that felt <i>amazing</i>.|#258|
@wait 1.5
GeraldFear: Even Betty, the love of my life, called me "Harold" at times...|#213|
@wait 1.5
@shake EvilPrinter count:2
GeraldFear: But, you...!<br>You remembered my name — first <i>and</i> last!|#214|
; play this with reverb, as it is inside of Gerald's head.
; Don't show Elgrim's sprite.
@hide GeraldFear
@hide EvilPrinter remove:true
@wait .4
@show BlackBG
@char Elgrim.NormalWithBG wait:false
@wait .3
Elgrim: Gerald, you moron!|#215|
Elgrim: This is <i>clearly</i> a trap!|#216|
@wait 1.5
@hide Elgrim,BlackBG remove:true
@hide AnimatedPrinter wait:false
@char GeraldFear.Horny
GeraldFear: ..................|#217|
; Gerald says this aloud.
GeraldFear: I don't |#259|<i>care</i>!|#218|
GeraldFear: {PlayerName}...<br>They said my full name!|#25a|
@wait 1.5
@stopBgm fade:0.5 wait:false
@bgm BGM_LoveTheme_Short
@back GayLighting_Oil_Bullets id:GayLighting tint:#FF69B4 time:4 wait:false
GeraldFear: Come here, you sexy bell pepper, you!|#21a|
@wait .3
@hide EvilPrinter
@hideChars remove:true
@hidePrinter EvilPrinter wait:false
@back id:WhiteBG time:3
@wait 1
@back id:SacrilegeCG wait:false
@char KissyChar.{PlayerCustomAppearance} pose:Speaking scale:0.50,0.50,0.50 wait:false
@camera zoom:0.392 ortho:true easing:SmoothStep wait:false
@camera zoom:0 time:6 
Player: G-Gerald...!|#25b|
; Show the CG of Gerald Ford and the player kissing.
I ran towards Gerald Ford,<br>my lack-of-heart beating at a breakneck pace.|#21b|
I grabbed the back of his neck, my lips meeting his<br>in a fiery display of pepper-on-demon action.|#21c|
As we embraced, it felt like time itself stopped, the holey oil<br>spilling down Gerald Ford's throat alongside my saliva.|#21d|
@wait 5
; Gerald's voice has reverb
@hide SacrilegeCG,KissyChar remove:true time:4
@hide AnimatedPrinter wait:false
@wait 2.5
@char GeraldFade.Fade1
GeraldFade: ...|#28e|
GeraldFade: Even if it was fake...|#21f|
@wait .4
@char GeraldFade.Fade2
GeraldFade: I'm just glad someone finally... remembered.|#222|
; Play a flash of light effect again as Gerald disappears. Or have his sprite fade away?
@char GeraldFade.Fade3
@wait 1.5
GeraldFade: T-thank you, |#223|{PlayerName}...<br>For everything...|#225|
@hide EvilPrinter wait:false

; Opportunity to let Gerald leave with a smile :)

@hide GeraldFade remove:true time:4
@hide GeraldOffice
@hide GayLighting
; Gerald Ford and Gerald Fear sprites go away. He is dead.
@stopBgm wait:false fade:4
; fade to black
@hideAll remove:true time:1
@back id:BlackBG
@wait 6
@hideAll remove:true
@hideChars remove:true
@wait 1
@goto Epilogue
@stop